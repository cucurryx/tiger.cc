let
  class Shape
  {
    /* Position.  */
    var row := 0
    var col := 0

    method print_row() = (print("row = "); print_int(self.row))
    method print_col() = (print("col = "); print_int(self.col))

    method print() =
      (
        print("Shape = { ");
        self.print_row();
        print(", ");
        self.print_col();
        print(" }")
      )
  }

  class Circle extends Shape
  {
    var radius := 1

    method print_radius() = (print("radius = "); print_int(self.radius))

    /* Overridden method.  */
    method print() =
      (
        print("Circle = { ");
        self.print_row();
        print(", ");
        self.print_col();
        print(", ");
        self.print_radius();
        print(" }")
      )
  }

  /* C has static type Shape, and dynamic (exact) type Circle.  */
  var c : Shape := new Circle
in
  /* Dynamic dispatch to Circle's print method.  */
  c.print();

  /* Allowed.  */
  c.print_row()

  /* Forbidden: `print_radius' is not a member of Shape (nor of its
     super class(es)).  */
  /* c.print_radius() */
end